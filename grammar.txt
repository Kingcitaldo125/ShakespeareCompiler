S -> title dramatis-personae actlist

marticles -> ACTOR | PAUL | SHAKESPEARE | ACT | SCENE | YOU | ARTICLE | io | POSSESSIVE | lambda
marticles -> _stackop | MORE | MOST | LESS | SUM | OF | AND | THE | TOO | TO | WE | IN | IF | IS | SO | DO | LET | US
marticles -> WE | SHALL | MUST | OUGHT | WE | OR | BETWEEN | HALF | OPPOSITE | NOT | DIFFERENT | FROM
marticles -> THAN | THEN | BETTER | WORSE | EXITS | EXIT | RECALLS | REMEMBERS | EMAN | PROCLAM
marticles -> POS_ADJ | NEG_ADJ | POS_NOUN | NEG_NOUN

prime_wordlist -> marticles wordlist marticles | marticles | wordlist

title -> prime_wordlist title | prime_wordlist peq

multiple_words -> prime_wordlist multiple_words | prime_wordlist
peq -> PERIOD | QUESTION | EXCLAMATION
pe -> PERIOD | EXCLAMATION

dramatis-personae -> ACTOR COMMA multiple_words peq dramatis-personae
dramatis-personae -> ACTOR COMMA multiple_words peq

actlist -> act actlist | act

act -> ACT SOME_ROMAN_NUMBERS COLON multiple_words peq scenelist
act -> ACT I COLON multiple_words peq scenelist

scenelist -> scene scenelist | scene
scene -> SCENE SOME_ROMAN_NUMBERS COLON multiple_words peq statement-list
scene -> SCENE I COLON multiple_words peq statement-list

mact -> ACT I COLON multiple_words peq
mact -> ACT SOME_ROMAN_NUMBERS COLON multiple_words peq
mscene -> SCENE SOME_ROMAN_NUMBERS COLON multiple_words peq
mscene -> SCENE I COLON multiple_words peq

statement-list -> statement statement-list | statement
statement -> enter-stmt | exit-stmt | line | mact | mscene

line -> ACTOR COLON optional-addressee sentence-list
line -> ACTOR COLON sentence-list

optional-addressee -> ACTOR COMMA
optional-interjection -> interjection COMMA

interjection -> NAY | YEA | VERILY | FORSOOTH | PERADVENTURE | PERCHANCE

sentence-list -> sentence sentence-list | sentence
sentence-list -> optional-interjection sentence sentence-list | optional-interjection sentence

sentence -> io pe | assignment pe | goto pe | _stackop pe | stackop pe | question QUESTION | conditional

io -> SPEAK_YOUR_MIND | OPEN_YOUR_MIND | OPEN_YOUR_HEART | LISTEN_TO_YOUR_HEART

enter-stmt -> LBRACKET ENTER ACTOR RBRACKET
enter-stmt -> LBRACKET ENTER actorlist RBRACKET

exit-stmt -> LBRACKET EXIT ACTOR RBRACKET
exit-stmt -> LBRACKET EXEUNT actorlist RBRACKET
exit-stmt -> LBRACKET EXEUNT OMNES RBRACKET
exit-stmt -> LBRACKET EXEUNT RBRACKET

assignment -> YOU ARE AS adj AS arithmetic
assignment -> YOU ARE arithmetic
assignment -> YOU value
assignment -> I AM arithmetic

arithmetic -> ARTICLE value | THE value | POSSESSIVE value | YOU | MYSELF | YOURSELF | ACTOR
arithmetic -> NOTHING
arithmetic -> THE SUM OF arithmetic AND arithmetic
arithmetic -> THE DIFFERENCE BETWEEN arithmetic AND arithmetic
arithmetic -> THE PRODUCT OF arithmetic AND arithmetic
arithmetic -> THE QUOTIENT OF arithmetic AND arithmetic
arithmetic -> THE QUOTIENT BETWEEN arithmetic AND arithmetic
arithmetic -> THE REMAINDER OF arithmetic AND arithmetic
arithmetic -> THE REMAINDER BETWEEN arithmetic AND arithmetic
arithmetic -> THE SQUARE OF arithmetic
arithmetic -> THE CUBE OF arithmetic
arithmetic -> THE SQUARE ROOT OF arithmetic
arithmetic -> TWICE arithmetic
arithmetic -> THRICE arithmetic
arithmetic -> HALF arithmetic
arithmetic -> BOTH arithmetic AND arithmetic
arithmetic -> EITHER arithmetic OR arithmetic
arithmetic -> THE OPPOSITE OF arithmetic

value -> adj-list noun | noun

actorlist -> ACTOR COMMA actorlist | ACTOR AND ACTOR
noun -> POS_NOUN | NEG_NOUN
adj -> POS_ADJ | NEG_ADJ
adj-list -> adj adj-list | adj

let_us -> LET US | WE SHALL | WE MUST | WE OUGHT TO
goto -> let_us PROCEED TO goto-target
goto -> let_us PROCEED TO multiple_words
goto -> let_us PROCEED POSTHASTE TO goto-target
goto -> let_us PROCEED POSTHASTE TO multiple_words
goto -> let_us RETURN

goto-target -> ACT SOME_ROMAN_NUMBERS COMMA SCENE SOME_ROMAN_NUMBERS
goto-target -> ACT I COMMA SCENE SOME_ROMAN_NUMBERS
goto-target -> ACT SOME_ROMAN_NUMBERS COMMA SCENE I
goto-target -> ACT I COMMA SCENE I
goto-target -> ACT SOME_ROMAN_NUMBERS
goto-target -> ACT I

stackop -> REMEMBER arithmetic
_stackop -> RECALL wordlist
_stackop -> RECALL multiple_words

question -> AM I comparison
question -> IS arithmetic comparison
question -> ARE YOU comparison

comparison -> BETTER THAN arithmetic
comparison -> MORE adj THAN arithmetic
comparison -> AS adj AS arithmetic
comparison -> WORSE THAN arithmetic
comparison -> LESS adj THAN arithmetic
comparison -> AT LEAST AS adj AS arithmetic
comparison -> AT MOST AS adj AS arithmetic
comparison -> DIFFERENT FROM arithmetic

conditional -> IF SO COMMA optional-interjection sentence
conditional -> IF SO COMMA sentence
conditional -> IF NOT COMMA optional-interjection sentence
conditional -> IF NOT COMMA sentence
